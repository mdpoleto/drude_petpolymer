* $Id: test_drude_lipid.str 28 2015-07-29 23:54:15Z alex $
* Test case, Drude lipid toppar file
*

set boml 0 ! set to -1 if the series contains 3-membered rings

set nres 13

set resi1  dmpl
set resi2  glyc
set resi3  glyp
set resi4  pe
set resi5  pc
set resi6  dmpc
set resi7  dppc
set resi8  dopc
set resi9  dppe
set resi10 popc
set resi11 dope
set resi12 pope
set resi13 dlpc
set seed1  1 O11 1 P   1 O12
set seed2  1 O8  1 C2  1 C1
set seed3  1 CP1 1 OP1 1 P1
set seed4  1 HN1 1 N   1 C1
set seed5  1 C4  1 N   1 C1
set seed6  1 C15 1 N   1 C12
set seed7  1 C15 1 N   1 C12
set seed8  1 C15 1 N   1 C12
set seed9  1 N   1 C12 1 C11
set seed10 1 C13 1 N   1 C12
set seed11 1 N   1 C12 1 C11
set seed12 1 N   1 C12 1 C11
set seed13 1 C15 1 N   1 C12
!set seed11 1 HN1 1 N   1 C12
!set seed12 1 HN1 1 N   1 C12

!!set count 1
set count 5
label loop_lipid1

set residue @resi@@count
read sequence @residue 1

bomlev @boml
generate @residue first none last none setup warn drude dmass 0.4 !show
bomlev 0

ic param
ic seed @seed@@count
ic build 
coor print sele .not. init .and. .not. type d* end
coor sdrude
coor shake

! instead of relaxing Drude, move all Drude a little bit away from their parent atom
coor trans xdir -0.002 ydir -0.002 zdir -0.002 sele type D* end

update inbfrq -1 ihbfrq 0 switch atom vatom vswitch cutnb 999.0 ctofnb 997.0 ctonnb 995.0

if @mindr eq 0 then
  energy
 else
  cons harm force 100000. sele .not. type D* end
  mini ABNR nstep 200 nprint 20
  cons harm clear
 endif
echo @residue starting E: ?ener

if @?pdbdir eq 1 then
  write psf card name @pdbdir/@residue.psf
  write coor pdb name @pdbdir/@residue_start.pdb
 endif

if @mini ne 0 then
  mini sd nstep 1000 tolgrd 0.0001 nprint 100
  if ?grms gt 0.0001 mini abnr nstep 1000 tolgrd 0.0001
  if ?grms gt 0.0001 then
    echo FATAL ERROR: MINIMIZATION NOT CONVERGED
    echu
    echo FATAL ERROR: MINIMIZATION NOT CONVERGED
    stop
   endif
  echo @residue minimized E: ?ener
  if @?pdbdir eq 1 then ! cannot use 1-line syntax because substitution will fail
    write coor pdb name @pdbdir/@residue_min.pdb
   endif
 endif

coor orient
coor dipole oxyz select all end 
echo @residue dipole: ?rdip
!! Check molecular polarizability
!VIBRAN
!diag dscf fini step 0.001
!END

shake off
drude reset
lone clear
delete atom sele all end

incr count
if count le @nres goto loop_lipid1
